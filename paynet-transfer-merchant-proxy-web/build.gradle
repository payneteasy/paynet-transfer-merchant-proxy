buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.owasp:dependency-check-gradle:${property('owasp.dependencyCheck.version')}"
    }
}

apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'

group = 'com.payneteasy.transfer-merchant-proxy'

buildDir = 'target'

war {
    archiveName = 'ROOT.war'
}

repositories {
    mavenCentral()
    mavenLocal()
}

if (!project.hasProperty("profile")) {
    ext.profile = "local"
}

dependencies {

    def springVersion = '5.3.32'
    def slf4jVersion = '2.0.12'
    def tomcatVersion = '8.0.28'
    def guavaVersion = '33.0.0-jre'
    def logbackVersion = '1.5.0'
    def junitVersion = '4.12'
    def jacksonVersion = '2.16.1'
    def hibernateValidatorVersion = '6.2.5.Final'
    def h2dbVersion = '2.2.224'

    compile "com.google.guava:guava:${guavaVersion}" //Justification: useful utility classes for Google libraries

    compile "ch.qos.logback:logback-classic:${logbackVersion}" //Justification: logback api for application logging

    compile "org.springframework:spring-core:${springVersion}", //Justification: core dependencies for spring - application framework
            "org.springframework:spring-beans:${springVersion}", //Justification: dependencies of spring used for configuration purposes
            "org.springframework:spring-context:${springVersion}", //Justification: dependencies for spring context configuration
            "org.springframework:spring-webmvc:${springVersion}", //Justification: dependencies for web app development
            "org.springframework:spring-context-support:${springVersion}", //Justification: dependencies for spring context support
            "org.springframework:spring-jdbc:${springVersion}" //Justification: for spring database integration

    compile "com.h2database:h2:${h2dbVersion}" //Justification: database for application

    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}" //Justification: for work with json protocol
    compile "org.hibernate:hibernate-validator:${hibernateValidatorVersion}" //Justification: for validation purposes

    compile "org.slf4j:slf4j-api:${slf4jVersion}", //Justification: api for application logging
            "org.slf4j:jcl-over-slf4j:${slf4jVersion}" //Justification: Jakarta/Apache commons logging 1.2 implemented over SLF4J

    testCompile "org.springframework:spring-test:${springVersion}" //Justification: spring module for framework based testing
    testCompile "junit:junit:${junitVersion}" //Justification: library for project unit tests
    testCompile "org.hamcrest:hamcrest-all:1.3", //Justification: matcher framework for tests
            "com.jayway.jsonpath:json-path:2.2.0", //Justification: for json verification tests
            "org.easymock:easymock:3.4" //Justification: to mock entities in unit tests

    providedCompile 'javax.servlet:javax.servlet-api:3.1.0' //Justification: servlet apis for web controllers

    testCompile "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}", //Justification: embedded http server for test cases
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}", //Justification: logging for embedded http server
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}", //Justification: core embedded http server impl
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}", //Justification: core embedded http server impl
            "org.apache.tomcat:tomcat-jasper:${tomcatVersion}", //Justification: core http server impl
            "org.apache.tomcat:tomcat-jasper-el:${tomcatVersion}", //Justification: expression language for embedded http server impl
            "org.apache.tomcat:tomcat-jsp-api:${tomcatVersion}" //Justification: JSP package for embedded http server

    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.6.1' //Justification: JSON API documentation for spring based application
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.6.1' //Justification: JSON API documentation for spring based application (UI)
    compile group: 'io.springfox', name: 'springfox-schema', version: '2.6.1' //Justification: JSON API documentation schemes for spring based application
}

sourceSets {
    main {
        resources {
            srcDir "src/main/profiles/${profile}"
        }
    }
}

configurations {
    testArtifacts
}

task testJar(type: Jar) {
    baseName = "${project.name}-test"
    from sourceSets.test.output
}

artifacts {
    testArtifacts testJar
}

dependencyCheck {
    format = "${property('owasp.dependencyCheck.format')}"
    suppressionFile = project.file("suppress.xml").absolutePath
}
