buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:4.0.2'
    }
}

apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'

group = 'com.payneteasy.transfer-merchant-proxy'

buildDir = 'target'

war {
    archiveName = 'ROOT.war'
}

repositories {
    mavenCentral()
    mavenLocal()
}

if (!project.hasProperty("profile")) {
    ext.profile = "local"
}

dependencies {

    def springVersion = '4.3.22.RELEASE'
    def slf4jVersion = '1.7.25'
    def tomcatVersion = '8.0.28'
    def guavaVersion = '27.1-jre'
    def logbackVersion = '1.2.2'
    def junitVersion = '4.12'
    def jacksonVersion = '2.9.8'
    def hibernateValidatorVersion = '5.4.1.Final'
    def h2dbVersion = '1.4.195'

    compile "com.google.guava:guava:${guavaVersion}"

    compile "ch.qos.logback:logback-classic:${logbackVersion}"

    compile "org.springframework:spring-core:${springVersion}",
            "org.springframework:spring-beans:${springVersion}",
            "org.springframework:spring-context:${springVersion}",
            "org.springframework:spring-webmvc:${springVersion}",
            "org.springframework:spring-context-support:${springVersion}",
            "org.springframework:spring-jdbc:${springVersion}"

    compile "com.h2database:h2:${h2dbVersion}"

    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "org.hibernate:hibernate-validator:${hibernateValidatorVersion}"

    compile "org.slf4j:slf4j-api:${slf4jVersion}",
            "org.slf4j:jcl-over-slf4j:${slf4jVersion}"

    testCompile "org.springframework:spring-test:${springVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.hamcrest:hamcrest-all:1.3",
                "com.jayway.jsonpath:json-path:2.2.0",
                "org.easymock:easymock:3.4"

    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}",
            "org.apache.tomcat:tomcat-jasper:${tomcatVersion}",
            "org.apache.tomcat:tomcat-jasper-el:${tomcatVersion}",
            "org.apache.tomcat:tomcat-jsp-api:${tomcatVersion}"

    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.6.1'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.6.1'
    compile group: 'io.springfox', name: 'springfox-schema', version: '2.6.1'
}

sourceSets {
    main {
        resources {
            srcDir "src/main/profiles/${profile}"
        }
    }
}

configurations {
    testArtifacts
}

task testJar(type: Jar) {
    baseName = "${project.name}-test"
    from sourceSets.test.output
}

artifacts {
    testArtifacts testJar
}

configurations {
    depCheck
}

dependencies {
    depCheck "org.owasp:dependency-check-ant:4.0.2"
}

task checkDeps { doLast {
    ant.taskdef(name: 'checkDepsTask',
            classname: 'org.owasp.dependencycheck.taskdefs.Check',
            classpath: configurations.depCheck.asPath)

    ant.checkDepsTask(projectName: "gasicredit-${version}") {
        path {
            pathelement(path: sourceSets.main.runtimeClasspath.asPath)
        }
    }
}}
